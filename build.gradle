apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'maven'
//java
sourceCompatibility=1.7
//war
war.baseName='lnramirez'
//jetty
jettyRun.scanIntervalSeconds=1
jettyRun.httpPort=8282
jettyRun.stopPort=8081
jettyRun.stopKey='s'
//project
group='com.bajoneando.lnramirez'
version='0.1.6-SNAPSHOT'
description='Official lnramirez site'
//repositories
repositories {
    mavenCentral()
    mavenLocal()
    mavenRepo urls: [
        "http://repo.springsource.org/libs-release",
        "http://scala-tools.org/repo-releases"]
}
//dependencies
dependencies {
    
    compile "org.springframework:spring-core:$springVersion"
    compile ("org.springframework.data:spring-data-mongodb:$springMongoVersion") {
        exclude group: "org.springframework"
    }
    compile "org.springframework:spring-web:$springVersion"
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.springframework:spring-oxm:$springVersion"
    compile "org.springframework.security:spring-security-core:$springSecurityVersion"
    compile "org.springframework.security:spring-security-web:$springSecurityVersion"
    compile "org.springframework.security:spring-security-config:$springSecurityVersion"
    compile "org.springframework.security:spring-security-taglibs:$springSecurityVersion"
    //compile "org.springframework.security:spring-security-openid:$springSecurityVersion"
    compile "com.thoughtworks.xstream:xstream:$xstreamVersion"
    compile "org.codehaus.jackson:jackson-jaxrs:$jacksonVersion"
    compile "jstl:jstl:$jstlVersion"
    compile "taglibs:standard:$standardVersion"
    compile "org.markdownj:markdownj:$markdownjVersion"
    compile "com.bajoneando:bajoneandotags:$bajoneandoTagsVersion"
    
    providedCompile "javax.servlet:servlet-api:$servletapiVersion"
    providedCompile "javax.servlet.jsp:jsp-api:$jspapiVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "org.mockito:mockito-core:$mockitoCoreVersion"

    runtime "org.tuckey:urlrewritefilter:$urlrewritefilterVersion"
    runtime "opensymphony:sitemesh:$sitemeshVersion"
    runtime "xerces:xercesImpl:$xercesVersion"
    runtime "commons-fileupload:commons-fileupload:$commonsfileuploadVersion"
    runtime "commons-io:commons-io:$commonsioVersion"   
    //logging
    providedCompile "commons-logging:commons-logging:$commonsloggingVersion"
    compile "org.slf4j:slf4j-api:$slf4japiVersion"
    runtime "org.slf4j:jcl-over-slf4j:$jcloverslf4jVersion"
    runtime "org.slf4j:slf4j-log4j12:$slf4jlog4j12Version"
    runtime "log4j:log4j:$log4jVersion"
}

//tasks
task createDirSources << {
    sourceSets.all {
        (java.srcDirs + resources.srcDirs + webAppDir).each { _srcDir ->
            if (!_srcDir.exists()) {
                println("mkdirs ${_srcDir.canonicalPath}")
                _srcDir.mkdirs()
            }
        }
    }   
}

//maven 
task copyPomFiles (dependsOn: 'install') << {
    println "copying build/poms/pom-default.xml to pom.xml"
    ant.copy(file: "build/poms/pom-default.xml", tofile: "pom.xml")
    println "copied"
}

optionalDeps = []
providedDeps = []

optional = { optionalDeps << it }
provided = { providedDeps << it }

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // respect 'optional' and 'provided' dependencies
        gradleProject.optionalDeps.each { dep ->
            generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
        }
        gradleProject.providedDeps.each { dep ->
            generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
        }
        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            organization {
                name = 'bajoneando'
            }
            build {
                plugins {
                    plugin {
                        groupId = 'org.apache.maven.plugins'
                        artifactId = 'maven-compiler-plugin'
                        configuration {
                            source = '1.6'
                            target = '1.6'
                        }
                    }
                    plugin {
                        groupId = 'org.apache.maven.plugins'
                        artifactId = 'maven-surefire-plugin'
                        configuration {
                            includes {
                                include = '**/*Tests.java'
                            }
                            excludes {
                                exclude = '**/*Abstract*.java'
                            }
                        }
                    }
                }
                resources {
                    resource {
                        directory = 'src/main/java'
                        includes = ['**/*']
                        excludes = ['**/*.java']
                    }
                    resource {
                        directory = 'src/main/resources'
                        includes = ['**/*']
                    }
                }
                testResources {
                    testResource {
                        directory = 'src/test/java'
                        includes = ['**/*']
                        excludes = ['**/*.java']
                    }
                    testResource {
                        directory = 'src/test/resources'
                        includes = ['**/*']
                    }
                }
            }
            developers {
                developer {
                    id = 'lnramirez'
                    name = 'Luis Ramirez Monterosa'
                    email = 'lnramirez@gmail.com'
                }
            }
        }
    }
}    
